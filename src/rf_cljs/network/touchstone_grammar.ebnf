(* Non/terminal naming conventions:
    FirstLetterCapitalCamelCase = nonterminals 
    firstLetterLowerCamelCase = terminals used by many nonterminals 
    snake_case = terminals used by one nonterminal, "local" terminals *)

<File> = Version1 | Version2

Version1 =
OptionLine
FrequencyPoint+
<newline>?

Version2 =
 <Version>
 OptionLine
 NumberOfPorts
(TwoPortDataOrder | NumberOfFrequencies | NumberOfNoiseFrequencies | Reference | MatrixFormat)+
 NetworkData
 NoiseData?
 <End>
 <newline>?


Version = <"[Version] 2.0">  <newline>

OptionLine = <hash> Option*  <newline>
<Option> = <space> (FrequencyUnit | Format | Parameter | OptionReference)
FrequencyUnit = (#"(?i)Hz" | #"(?i)kHz" | #"(?i)MHz" | #"(?i)GHz")
Parameter = (#"(?i)S" | #"(?i)Y" | #"(?i)Z" | #"(?i)G" | #"(?i)H")
Format = (#"(?i)DB" | #"(?i)MA" | #"(?i)RI")
OptionReference = <#"(?i)R"> <space> positiveFloatingPoint

NumberOfPorts = <"[Number of Ports] "> positiveNonZeroInt  <newline>

TwoPortDataOrder = <"[Two-Port Data Order] "> tpdo_arg  <newline>
<tpdo_arg> = "12_21" | "21_12"

NumberOfFrequencies = <"[Number of Frequencies] "> positiveNonZeroInt  <newline>

NumberOfNoiseFrequencies = <"[Number of Noise Frequencies] "> positiveNonZeroInt  <newline>

Reference = <"[Reference]"> <#"\s"?> positiveFloatingPoint <#"\s"?> (positiveFloatingPoint | <#"\s">)+

MatrixFormat = <"[Matrix Format] "> matrix_formats  <newline>
<matrix_formats> = "Full" | "Lower" | "Upper"

NetworkData = <"[Network Data]"> <newline> FrequencyPoint+

NoiseData = <"[Noise Data]"> <newline> FrequencyPoint+


FrequencyPoint =  OddDataLine EvenDataLine*

(* Elijah! *)
<EvenData> = floatingPoint <space> floatingPoint (<space> EvenData)?
OddDataLine = floatingPoint <space> EvenData <newline>
EvenDataLine =  EvenData <newline>

<End> = "[End]" newline

<positiveNonZeroInt> = #"(0*[1-9][0-9]*)"
<positiveFloatingPoint> = #"[0-9]*\.?[0-9]+([eE][-+]?[0-9]+)?"
<floatingPoint> = #"[-+]?[0-9]*\.?[0-9]+([eE][-+]?[0-9]+)?"
<hash> = "#"
<space> = " "
<newline> = "\n" | "\r" | "\r\n"

