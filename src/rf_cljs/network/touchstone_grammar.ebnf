(* Non/terminal naming conventions:
    FirstLetterCapitalCamelCase = nonterminals 
    firstLetterLowerCamelCase = terminals used by many nonterminals 
    snake_case = terminals used by one nonterminal, "local" terminals *)

<File> = Version1 | Version2

Version1 =
<(CommentLine)*>
OptionLine
Data
<(whitespace | newline)*>

Version2 =
<(CommentLine)*> <Version>
<(CommentLine)*> OptionLine
<(CommentLine)*> NumberOfPorts
(TwoPortDataOrder | NumberOfFrequencies | NumberOfNoiseFrequencies | Reference | MatrixFormat)+
<(CommentLine)*> NetworkData
<(CommentLine)*> NoiseData?
<(CommentLine)*> <End>
<(whitespace | newline)*>

<Comment> = #"!.*"

Version = <"[Version] 2.0"> <Comment?> <newline>

OptionLine = <hash> <whitespace>* Option* <Comment?> <newline>
<Option> = FrequencyUnit | Format | Parameter | OptionReference
FrequencyUnit = (#"(?i)Hz" | #"(?i)kHz" | #"(?i)MHz" | #"(?i)GHz") <whitespace>+
Parameter = (#"(?i)S" | #"(?i)Y" | #"(?i)Z" | #"(?i)G" | #"(?i)H") <whitespace>+
Format = (#"(?i)DB" | #"(?i)MA" | #"(?i)RI") <whitespace>+
OptionReference = <#"(?i)R"> <whitespace>+ positiveFloatingPoint

NumberOfPorts = <"[Number of Ports] "> positiveNonZeroInt <Comment?> <newline>

TwoPortDataOrder = <"[Two-Port Data Order] "> tpdo_arg <Comment?> <newline>
<tpdo_arg> = "12_21" | "21_12"

NumberOfFrequencies = <"[Number of Frequencies] "> positiveNonZeroInt <Comment?> <newline>

NumberOfNoiseFrequencies = <"[Number of Noise Frequencies] "> positiveNonZeroInt <Comment?> <newline>

Reference = <"[Reference]"> <#"\s"?> positiveFloatingPoint <#"\s"?> (positiveFloatingPoint | <#"\s">)+

MatrixFormat = <"[Matrix Format] "> matrix_formats <Comment?> <newline>
<matrix_formats> = "Full" | "Lower" | "Upper"

NetworkData = <"[Network Data]"> <(whitespace* newline)?> Data

NoiseData = <"[Noise Data]"> <(whitespace* newline)?> Data

DataLine = (<whitespace*> floatingPoint <whitespace*>)+ <Comment?> <newline>

<CommentLine> = Comment? newline

Data = (DataLine | <CommentLine>)+

<End> = "[End]" newline

<positiveNonZeroInt> = #"(0*[1-9][0-9]*)"
<positiveFloatingPoint> = #"([0-9]*[.])?[0-9]+"
<floatingPoint> = #"(-)?([0-9]*[.])?[0-9]+"
<hash> = "#"
<whitespace> = " " | "\t"
<newline> = "\n" | "\r" | "\r\n"

